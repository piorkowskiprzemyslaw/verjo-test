buildscript {
    ext {
        repoConfig = {
            mavenLocal()

            mavenCentral()

            maven {
                url 'https://plugins.gradle.org/m2/'
            }
        }

        jooqVersion = "3.11.5"
        gradleJooqPluginVersion = "3.0.2"
    }

    repositories repoConfig

    dependencies {
        classpath("nu.studer:gradle-jooq-plugin:${gradleJooqPluginVersion}")
        classpath("com.vertabelo.gradle:vertabelo:1.0.1")
        classpath("javax.xml.bind:jaxb-api:2.3.0")
        classpath("com.sun.xml.bind:jaxb-core:2.3.0")
        classpath("com.sun.xml.bind:jaxb-impl:2.3.0")
        classpath("javax.activation:activation:1.1.1")
    }
}

apply plugin: "java"
apply plugin: "nu.studer.jooq"
apply plugin: "vertabelo"

repositories repoConfig

dependencies {
    compile("org.jooq:jooq:${jooqVersion}")

    jooqRuntime("com.github.piorkowskiprzemyslaw:verjo:+")
    jooqRuntime("javax.xml.bind:jaxb-api:2.3.0")
    jooqRuntime("com.sun.xml.bind:jaxb-core:2.3.0")
    jooqRuntime("com.sun.xml.bind:jaxb-impl:2.3.0")
    jooqRuntime("javax.activation:activation:1.1.1")
}

vertabelo {
    destSQLFile = "schema/model.sql"
    destXMLFile = "schema/model.xml"
    apiToken = System.getProperty("verjo-test.apiToken", "TODO-TOKEN")
    modelId = System.getProperty("verjo-test.modelId", "TODO-MODEL-ID")
}

task updateVertabelo(dependsOn: [vertabeloSQL, vertabeloXML])

jooq {
    version = jooqVersion
    edition = 'OSS'
    sample(sourceSets.main) {
        generator {
            database {
                name = 'com.github.piorkowskiprzemyslaw.verjo.VertabeloDbDefinition'
                properties {
                    property {
                        key = "vertabelo-xml-file"
                        value = "schema/model.xml"
                    }

                    property {
                        key = "vertabelo-default-schema"
                        value = "default-schema-name"
                    }
                }
            }

            generate {
                relations = true
                deprecated = false
                records = true
                immutablePojos = true
                fluentSetters = true
            }

            target {
                packageName = "com.github.piorkowskiprzemyslaw.verjo.test"
                directory = "gen/main/java/vertabelo-test"
            }
        }
    }
}

clean {
    delete "gen"
}

task wrapper(type: Wrapper) {
    gradleVersion = "4.8"
    distributionType = Wrapper.DistributionType.ALL
}